/* Copyright (c) 2023 - 2024, Oracle and/or its affiliates. All rights reserved. */
/* Licensed under the Universal Permissive License v 1.0 as shown at https://oss.oracle.com/licenses/upl/. */

/* Souffle datalog rules to assist in authoring in-toto policies.*/

/**
 *  This relation provides the external parameters of a SLSA v1 provenance generated by npm.
    The external parameters include details of the triggering hosted build service workflow.

    Here is the related section of an example predicate we process in this relation:
        "externalParameters": {
            "workflow": {
                "ref": "refs/heads/main",
                "repository": "https://github.com/npm/node-semver",
                "path": ".github/workflows/release.yml"
            }
        },

    Parameters:
        component_id: number
            The target software component id.
        purl: symbol
            The Package URL identifier for the provenance subject.
        ref: symbol
            The Git reference.
        repository: symbol
            The repository URL.
        path: symbol
            The GitHub Actions workflow path.

 */
.decl slsa_v1_npm_external_parameters(component_id: number, purl: symbol, ref: symbol, repository: symbol, path: symbol)

slsa_v1_npm_external_parameters(component_id, purl, ref, repository, path):-
    provenance(prov_id, component_id, _, _, _, _),
    statement(stmt_id, prov_id, "https://in-toto.io/Statement/v1", "https://slsa.dev/provenance/v1"),
    subject(sub_id, stmt_id, purl),
    predicate(pred_id, stmt_id),
    build_definition(build_id, pred_id, _),
    external_parameters(external_params_id, build_id),
    workflow(_, external_params_id, ref, repository, path).

/**
 *  This relation provides the external parameters of a SLSA v1 provenance generated by npm.
    The external parameters include details of the triggering hosted build service workflow.

    Here is the related section of an example predicate we process in this relation:
        "internalParameters": {
            "github": {
                "event_name": "push",
                "repository_id": "1357199",
                "repository_owner_id": "6078720"
            }
        },


    Parameters:
        component_id: number
            The target software component id.
        purl: symbol
            The Package URL identifier for the provenance subject.
        github_event_name: symbol
            TODO
        github_repository_id: symbol
            TODO
        github_repository_owner_id: symbol
            TODO

 */
.decl slsa_v1_npm_internal_parameters(
    component_id: number,
    purl: symbol,
    github_event_name: symbol,
    github_repository_id: symbol,
    github_repository_owner_id: symbol
)

slsa_v1_npm_internal_parameters(
    component_id,
    purl,
    github_event_name,
    github_repository_id,
    github_repository_owner_id
):-
    provenance(prov_id, component_id, _, _, _, _),
    statement(stmt_id, prov_id, "https://in-toto.io/Statement/v1", "https://slsa.dev/provenance/v1"),
    subject(sub_id, stmt_id, purl),
    predicate(pred_id, stmt_id),
    build_definition(build_id, pred_id, _),
    internal_parameters(_, build_id, github_event_name, github_repository_id, github_repository_owner_id).
