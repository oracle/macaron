/* Copyright (c) 2023 - 2024, Oracle and/or its affiliates. All rights reserved. */
/* Licensed under the Universal Permissive License v 1.0 as shown at https://oss.oracle.com/licenses/upl/. */

/* Souffle datalog rules to assist in policy authoring */

/**
 * Existence of this fact indicates a check with the id check_name was passed on the component with component_id.
 */
.decl check_passed(component_id: number, check_name: symbol)
check_passed(component_id, check_name) :- check_result(_, check_name, 1, component_id).

.decl check_failed(component_id: number, check_name: symbol)
check_failed(component_id, check_name) :- check_result(_, check_name, 0, component_id).


/**
 * This relation provides the passed checks together with the confidence score for a component.
   Parameters:
   component_id: number
       The target software component id.
   check_name: symbol
       The check name that matches the ``^mcn_([a-z]+_)+([0-9]+)$`` regular expression.
   confidence: float
       The confidence score computed for the check result.
 */
.decl check_passed_with_confidence(component_id: number, check_name: symbol, confidence: float)
check_passed_with_confidence(component_id, check_name, confidence) :-
    check_result(check_id, check_name, 1, component_id),
    check_facts(check_id, confidence, component_id,_,_).

/**
 * This relation provides the failed checks together with the confidence score for a component.
   Parameters:
   component_id: number
       The target software component id.
   check_name: symbol
       The check name that matches the ``^mcn_([a-z]+_)+([0-9]+)$`` regular expression.
   confidence: float
       The confidence score computed for the check result.
 */
.decl check_failed_with_confidence(component_id: number, check_name: symbol, confidence: float)
check_failed_with_confidence(component_id, check_name, confidence) :-
    check_result(check_id, check_name, 0, component_id),
    check_facts(check_id, confidence, component_id,_,_).


/**
 * Check name is valid.
 */
.decl is_check(check_name: symbol)
is_check(check_name) :- check_result(_, check_name, _, _).

/**
 *  This fact exists iff a repository is hosted on a trusted public platform.
 */
.decl not_self_hosted_git(repo_id:number, message:symbol)
not_self_hosted_git(repo_id, message) :- repository(repo_id, _, _, _, _, _, _, remote, _, _, _, _, _),
    match("^.*(github.com|gitlab.com).*$", remote), message=remote.

/**
 *  This fact exists iff a component with id dependency is a dependency of component with id parent.
 */
.decl transitive_dependency(parent: number, dependency: number)
transitive_dependency(parent, dependency) :- dependency(parent, dependency).
transitive_dependency(parent, dependency) :-
    transitive_dependency(parent, a), transitive_dependency(a, dependency).

/**
 *  Extract the id and PURL from the component relation.
 */
.decl is_component(component_id: number, purl: symbol)
 is_component(component_id, purl) :- component(component_id, purl,_, _, _, _, _, _, _).

/**
 *  Extract the id and full name from the repository relation.
 */
.decl is_repo(repo_id: number, repo_complete_name: symbol, component_id: number)
 is_repo(repo_id, repo_complete_name, component_id) :- repository(repo_id, component_id, repo_complete_name, _, _, _, _, _, _, _, _, _, _).

/**
 *  Extract the id and URL from the repository relation.
 */
.decl is_repo_url(repo_id: number, url: symbol)
 is_repo_url(repo_id, url) :- repository(_, repo_id, _, _, _, _, _, url, _, _, _, _, _).


/**
 * A convenience relation to find analyses conducted on a specific repository.
 */
 .decl repository_analysis(analysis_id: number, component_id: number, repo_id: number, repo_name: symbol)
repository_analysis(analysis_id, component_id, repo_id, repo_name) :-
    component(component_id, _, analysis_id, _, _, _, _, _, _),
    is_repo(repo_id, repo_name, component_id).

/**
 * A convenience relation to find analyses conducted on a specific component.
 */
 .decl component_analysis(analysis_id: number, component_id: number)
component_analysis(analysis_id, component_id) :-
    component(component_id, _, analysis_id, _, _, _, _, _, _).

/**
 *  ADT recursively describing a JSON object.
 */
.type JsonType = Int {x : number}
         | String {x : symbol}
         | Float {x : float}
         | Bool {x : number}
         | null {}
         | Object {x: symbol, y : JsonType}
         | Array {x : number, y : JsonType}

/**
 * Rules for referring to parts of a json type using the full path name.
 */

.decl json(name: symbol, id: number, root: JsonType)


.decl json_path(j: JsonType, a: JsonType, key:symbol)

json_path(a, b, key) :- a = $Object(k, b), json(name,_,a), key=cat(name, cat(".", k)).
json_path(a, b, key) :- a = $Array(k, b), json(name,_,a), key=cat(name, cat("[", cat(to_string(k), "]"))).

json_path(a, b, key) :- a = $Object(k, b), json(_,_,c), json_path(c,a,kb), key=cat(cat(kb, "."),k).
json_path(a, b, key) :- a = $Array(k, b), json(_,_,c), json_path(c,a,kb),key=cat(kb, cat(cat("[",to_string(k)), "]")).

json_path(a, b,key) :- json_path(a,c,_), json_path(c, b, kb), key=kb.

.decl json_number(name: symbol, json:number, addr: symbol, k:number)
.decl json_symbol(name:symbol, json:number, addr: symbol, k:symbol)

json_number(name, js, addr, val) :- json(name, js, r), json_path(r, $Int(val), addr).
json_symbol(name, js, addr, val) :- json(name, js, r), json_path(r, $String(val), addr).
